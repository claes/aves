/*
 * Copyright (c) Claes Holmerson 2013, 2015. The program is licensed under GNU GPL v3. See LICENSE.txt for details.
 */

apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {}
        apply from: 'signing.gradle', to: config
    }
    compileSdkVersion 17
    buildToolsVersion "22.0.1"
    defaultConfig {
        applicationId "se.eliga.aves"
        minSdkVersion 14
        targetSdkVersion 16
        versionCode buildVersionCode()
        versionName buildVersionName()
        buildConfigField "java.lang.String", "BUILDTIME", "\"" + buildTime() + "\""

    }
    buildTypes {
        debug {
            buildConfigField "boolean", "REPORT_CRASHES", "true"
            buildConfigField "java.lang.String", "BUILDTYPE", "\"debug\""
            applicationIdSuffix ".debug"
        }
        release {
            buildConfigField "boolean", "REPORT_CRASHES", "false"
            buildConfigField "java.lang.String", "BUILDTYPE", "\"release\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.config
        }
    }
}

dependencies {
    compile project(':library')
    compile files('libs/smoothie-0.1.jar')
    compile files('libs/universal-image-loader-1.8.5-with-sources.jar')
    compile 'ch.acra:acra:4.6.1'
}
